resource "aws_apigatewayv2_api" "http_api_gateway" {
  name          = "${var.region_name}-api-gateway"
  protocol_type = "HTTP"

  # S3로 호스팅되는 웹 프론트가 이 API를 크로스오리진으로 호출해야 함.
  # HTTP API 네이티브 CORS가 OPTIONS 프리플라이트를 ALB/ECS까지 보내지 않고 여기서 바로 처리해줌
  cors_configuration {
    allow_origins = var.cors_allowed_origins
    allow_methods = ["GET", "POST", "OPTIONS"]
    allow_headers = ["content-type", "authorization"]
    max_age       = 300
  }
}

# VPC Link ENI 보안 그룹 (ALB는 이 SG를 소스로만 인바운드를 허용함)
resource "aws_security_group" "vpc_link_sg" {
  name   = "${var.region_name}-vpc-link-sg"
  vpc_id = var.vpc_id

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = { Name = "${var.region_name}-vpc-link-sg" }
}

# API Gateway와 내부 ALB를 연결하는 VPC Link
resource "aws_apigatewayv2_vpc_link" "main" {
  name               = "${var.region_name}-vpc-link"
  security_group_ids = [aws_security_group.vpc_link_sg.id]
  subnet_ids         = var.private_subnet_ids

  tags = { Name = "${var.region_name}-vpc-link" }
}

# ALB로 프록시하는 통합
resource "aws_apigatewayv2_integration" "alb" {
  api_id             = aws_apigatewayv2_api.http_api_gateway.id
  integration_type   = "HTTP_PROXY"
  integration_uri    = var.alb_listener_arn
  integration_method = "ANY"
  connection_type    = "VPC_LINK"
  connection_id      = aws_apigatewayv2_vpc_link.main.id
}

# 모든 경로/메서드를 ALB 통합으로 전달
resource "aws_apigatewayv2_route" "default" {
  api_id    = aws_apigatewayv2_api.http_api_gateway.id
  route_key = "$default"
  target    = "integrations/${aws_apigatewayv2_integration.alb.id}"
}

# 자동 배포 스테이지
resource "aws_apigatewayv2_stage" "default" {
  api_id      = aws_apigatewayv2_api.http_api_gateway.id
  name        = "$default"
  auto_deploy = true
}